# Bazel build
# C/C++ documentation: https://docs.bazel.build/versions/master/be/c-cpp.html

load("@com_github_ray_project_ray//bazel:ray.bzl", "flatbuffer_py_library")
load("@com_github_ray_project_ray//bazel:cython_library.bzl", "pyx_library")
load("@com_github_google_flatbuffers//:build_defs.bzl", "flatbuffer_cc_library")

FLATC_ARGS = [
    "--gen-object-api",
    "--gen-mutable",
    "--scoped-enums",
]

filegroup(
    name = "streaming_fbs_file",
    srcs = ["format/streaming.fbs"],
    visibility = ["//visibility:public"],
)

flatbuffer_cc_library(
    name = "streaming_fbs",
    srcs = ["format/streaming.fbs"],
    flatc_args = FLATC_ARGS,
    out_prefix = "format/",
)

flatbuffer_cc_library(
    name = "streaming_queue_fbs",
    srcs = ["queue/streaming_queue.fbs"],
    flatc_args = FLATC_ARGS,
    out_prefix = "queue/",
)

cc_library(
    name = "streaming_lib",
    srcs = glob([
        "*.cc", "queue/*.cc"
    ]),
    hdrs = glob([
        "*.h", "queue/*.h"
    ]),
    includes = ["."],
    deps = [
        ":streaming_fbs",
        ":streaming_queue_fbs",
        "//:ray_util",
        "//:raylet_lib",
        "//:ray_common",
        "//:core_worker_lib",
        "@boost//:asio",
        "@boost//:any",
        "@boost//:circular_buffer",
        "@boost//:thread",
        "@com_google_googletest//:gtest"
    ],
    visibility = ["//visibility:public"],
)

cc_binary(
    name = "libstreaming_java.so",
    srcs = glob(["lib/java/*.h",
                 "lib/java/*.cc"
           ]) + [":jni.h", ":jni_md.h"],
    includes = [
        ".",
        "streaming/src",
        "streaming/src/lib/java",
    ],
    linkshared = 1,
    linkstatic = 1,
    deps = [
        ":streaming_lib",
    ],
)

cc_library(
    name = "streaming_queue",
    srcs = glob([
        "queue/*.cc"
    ]),
    copts = ["-Wno-unused-function"],
    linkstatic = 0,
    deps = [
        "//:core_worker_lib",
    ],
)

cc_binary(
    name = "streaming_test_worker",
    srcs = [
        "test/direct_call_workers.cc",
    ],
    includes = [
        "streaming/src/test",
    ],
    copts = ["-Itest/"],
    deps = [
        ":streaming_lib",
    ],
)

cc_binary(
    name = "streaming_writer_tests_with_streamingqueue",
    srcs = [
        "test/streaming_writer_tests_with_streamingqueue.cc",
        "test/streaming_queue_tests_base.h",
    ],
    includes = [
        "streaming/src/test",
    ],
    copts = ["-Itest/"],
    deps = [
        ":streaming_lib",
    ],
)

genrule(
    name = "copy_jni_h",
    srcs = [
        "@bazel_tools//tools/jdk:jni_header",
    ],
    outs = ["jni.h"],
    cmd = "cp -f $< $@",
)

genrule(
    name = "copy_jni_md_h",
    srcs = select({
        "@bazel_tools//src/conditions:windows": ["@bazel_tools//tools/jdk:jni_md_header-windows"],
        "@bazel_tools//src/conditions:darwin": ["@bazel_tools//tools/jdk:jni_md_header-darwin"],
        "//conditions:default": ["@bazel_tools//tools/jdk:jni_md_header-linux"],
    }),
    outs = ["jni_md.h"],
    cmd = "cp -f $< $@",
)

genrule(
    name = "streaming-jni-darwin-compat",
    srcs = [":libstreaming_java.so"],
    outs = ["libstreaming_java.dylib"],
    cmd = "cp $< $@",
    output_to_bindir = 1,
)

filegroup(
    name = "streaming_java",
    srcs = select({
        "@bazel_tools//src/conditions:darwin": [":libstreaming_java.dylib"],
        "//conditions:default": [":libstreaming_java.so"],
    }),
    visibility = ["//visibility:public"],
)